apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
if (rootProject.file('local.properties').exists()) {
    properties.load(rootProject.file('local.properties').newDataInputStream())
}

bintray {
    user properties.getProperty('bintray_user')
    key properties.getProperty('bintray_key')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = publishingInfo.lib
        userOrg = publishingInfo.org
        websiteUrl = publishingInfo.site
        issueTrackerUrl = publishingInfo.tracker
        vcsUrl = publishingInfo.git
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId "${publishingInfo.group}.${publishingInfo.lib}"
            artifactId publishingInfo.lib
            name publishingInfo.lib
            url publishingInfo.site
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                connection publishingInfo.git
                developerConnection publishingInfo.git
                url publishingInfo.site
            }
        }
    }
}